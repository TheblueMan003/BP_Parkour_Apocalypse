package outro_1

npc blue, blake

def stop(){
    blue,blake = null
}

def initCharacter(){
    positioned(-85 243 -284){
        blue = npc(netherite_ingot)
        blue.disableoptimization()
        blue.setanimation{
            /tp @s ~ ~ ~ 180 0
            tag(blue)
            blue.update()
        }
    }
    positioned(-85 243 -288){
        blake = npc(netherite_scrap)
        blake.disableoptimization()
        blake.setanimation{
            /tp @s ~ ~ ~ 0 0
            tag(blake)
            blake.update()
        }
    }
}
def @reset reset(){
    /function pa_eject:remove/all
    beacon.create.stop()
    beacon.animate.stop()
    main.stop()
}
def phase1(){
    positioned(942 -19 108){
        facing(942 -19 100){
            /function pa_eject:summon/blake_alt
        }
    }
    with(@e[tag=character]){
        /function pa_eject:animations/animation.portal_exit/play
        tag(animated)
    }
    positioned(938 -19 108){
        facing(938 -19 100){
            /function pa_eject:summon/blue
        }
    }
    with(@e[tag=character,tag=!animated],true){
        /function pa_eject:animations/animation.portal_exit_alt/play
        tag(animated)
        /tp @s ~ ~-0.1 ~
    }
    
    schedule.add(50){
        with(@e[tag=animated]){
            /function pa_eject:remove/this
        }
        with(@a[tag=cutscene],true){
            fade.towhite(0,10,10)
            sound.play(sfx.earth_shockwave)
        }
        /fill 946 -18 108 934 -6 108 air replace minecraft:magenta_stained_glass_pane
        phase2()
    }
}
def blue_walk(){
    blue.setanimation(){
        /tp @s ~ ~ ~-0.1
        if (block(~ ~-0.1 ~ minecraft:cut_copper_stairs)){
            at(@s){
                /tp @s ~ ~-0.1 ~
            }
        }
        blue.update()
    }
    blue.setanimations(animationset.walk())
}
def phase2(){
    positioned(938 -19 104){
        facing(938 -19 80){
            blue = npc(netherite_ingot)
        }
        blue.disableoptimization()
        blue.setanimation{
            /tp @s ~ ~ ~ 180 0
            tag(blue)
            blue.update()
            INT tick
            tick ++
            if (tick > 20){
                blue_walk()
            }
        }
    }
    positioned(942 -19 105){
        blake = npc(netherite_sword)
        blake.disableoptimization()
        blake.setanimation{
            facing(@e[tag=blue,limit=1]){
                /tp @s ~ ~ ~ ~ 0
            }
            tag(blake)
            blake.update()
        }
    }
    with(@a[tag=endcutscene],true){
        dialog.show(outro_0, 4, pass){
            positioned(944 -21 94){
                blue = npc(netherite_pickaxe)
                blue.disableoptimization()
                blue.setanimation{
                    /tp @s ~ ~ ~ -90 0
                    blue.update()
                }
            }
            schedule.add(2){
                with(@a[tag=endcutscene],true){
                    dialog.show(outro_4, 13, pass){
                        phase3()
                    }
                }
            }
            with(@e[tag=rftc.cam],true){
                /tp @s 940.60 -20.00 94.51 -90.30 -1.08
            }
        }
    }
}
def phase3(){
    blue = null
    positioned(944 -21 94){
        facing(946 -21 94){
            /function pa_eject:summon/blue_alt
            with(@e[tag=character]){
                /function pa_eject:animations/animation.blue_fly/play
                tag(animated)
            }
        }
    }
    with(@a[tag=endcutscene],true){
        dialog.show(outro_17a, 1, pass){
            schedule.add(25){
                outro_2.start()
            }
            schedule.add(40){
                with(@a[tag=endcutscene]){
                    gamemode(adventure)
                }
                with(@e[tag=animated]){
                    /function pa_eject:remove/this
                }
                main.stop()
                stop()
            }
        }
    }
}

int playCutscene:=false
def @reset(){
    playCutscene = false
}
int @test intro_playable(){
    if (playCutscene){
        return 0
    }
    else{
        return 1
    }
}
def start(){
    if (!playCutscene){
        playCutscene = true
        forceload(948,-20,106,931,-22,99)
        tag(endcutscene)
        parkour.timerend()
        parkour.timerhide()
        fade.towhiteandthen(40,20,10){
            /tp @a[tag=endcutscene] 940 -19 97
            chunk.waitforall((948,-20,106),(931,-22,99)){
                main.start()
                schedule.add(50){
                    phase1()
                    with(@a[tag=endcutscene],true){
                        sound.play(minecraft:block.portal.travel,0.25,2)
                    }
                }
            }
        }
    }
    else{
        fade.towhiteandthen(40,20,10){
            recap.show_recap()
        }
    }
}
process main{
    def main(){
        with(@a[tag=cutscene,tag=outro]){
            /spectate @e[tag=rftc.cam,limit=1] @s
        }
        /function pa_eject:animation_loop
    }
    def onStop(){
        kill(@e[tag=rftc.cam])
        /fill 946 -18 108 934 -6 108 magenta_stained_glass_pane replace air
        forceunload(948,-20,106,931,-22,99)
    }
    def onStart(){
        /summon armor_stand 940 -17 97 {Tags:["rftc.cam"],NoGravity:1,Marker:1,Rotation:[0f,0f]}
        with(@a[tag=endcutscene],true){
            tag(@s,cutscene)
            tag(@s,outro)
            gamemode(spectator)
        }
    }
}